plugins {
    id 'com.jfrog.bintray' version '1.2'
    id 'maven-publish'
}

apply from: "$rootDir/gradle/java.gradle"

group 'com.palantir.typescript-service-generator'
version '0.0.1'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            artifact(sourceJar) {
                classifier 'sources'
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'releases'
        name = 'typescript-service-generator'
        userOrg = 'palantir'
        licenses = ['MIT']
        publications = ['bintray']
    }
}

bintrayUpload.onlyIf {
    System.getenv('BINTRAY_USER') && System.getenv('BINTRAY_KEY') && project.version ==~ /\d+\.\d+\.\d+/
}

bintrayUpload.dependsOn { generatePomFileForBintrayPublication }
bintrayUpload.dependsOn { sourceJar }
bintrayUpload.dependsOn { build }
